<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
               xmlns:s="library://ns.adobe.com/flex/spark" 
               xmlns:mx="library://ns.adobe.com/flex/mx"
               minWidth="955" minHeight="600" backgroundAlpha="0"
               frameRate="60"
               applicationComplete="onApplicationComplete()"
               click="onClick()">
    <fx:Script>
        <![CDATA[
            import mx.collections.ArrayCollection;
            import mx.events.ResizeEvent;
            
            import net.hires.debug.Stats;
            
            import sk.yoz.ycanvas.demo.starlingComponent.events.CanvasEvent;
            import sk.yoz.ycanvas.demo.starlingComponent.valueObjects.Mode;
            import sk.yoz.ycanvas.utils.TransformationUtils;
            
            import starling.core.Starling;
            import starling.display.Sprite;
            import starling.events.Event;
            
            private var engine:Starling;
            
            private var mapBig:YCanvasStarlingComponent;
            private var mapSmall:YCanvasStarlingComponent;
            
            private var modeMapQuest:Mode;
            private var modeOSM:Mode;
            private var modeMapBox:Mode;
            private var modeEsri:Mode;
            private var modeCloudMade:Mode;
            
            [Bindable]
            private var componentList:ArrayCollection;
            
            [Bindable]
            private var modeList:ArrayCollection;
            
            private function onApplicationComplete():void
            {
                var viewPort:Rectangle = new Rectangle(0, 0, stage.stageWidth, stage.stageHeight);
                engine = new Starling(starling.display.Sprite, stage, viewPort, stage.stage3Ds[0]);
                engine.start();
                engine.addEventListener(starling.events.Event.ROOT_CREATED, onRootCreated);
                
                modeMapQuest = new Mode;
                modeMapQuest.templates = Vector.<String>([
                    "http://otile1.mqcdn.com/tiles/1.0.0/osm/${level}/${x}/${y}.png",
                    "http://otile2.mqcdn.com/tiles/1.0.0/osm/${level}/${x}/${y}.png",
                    "http://otile3.mqcdn.com/tiles/1.0.0/osm/${level}/${x}/${y}.png",
                    "http://otile4.mqcdn.com/tiles/1.0.0/osm/${level}/${x}/${y}.png"]);
                
                modeOSM = new Mode;
                modeOSM.templates = Vector.<String>([
                    "http://a.tile.openstreetmap.org/${level}/${x}/${y}.png",
                    "http://b.tile.openstreetmap.org/${level}/${x}/${y}.png",
                    "http://c.tile.openstreetmap.org/${level}/${x}/${y}.png"]);
                
                modeMapBox = new Mode;
                modeMapBox.templates = Vector.<String>([
                    "http://a.tiles.mapbox.com/v3/examples.map-vyofok3q/${level}/${x}/${y}.png",
                    "http://b.tiles.mapbox.com/v3/examples.map-vyofok3q/${level}/${x}/${y}.png",
                    "http://c.tiles.mapbox.com/v3/examples.map-vyofok3q/${level}/${x}/${y}.png",
                    "http://d.tiles.mapbox.com/v3/examples.map-vyofok3q/${level}/${x}/${y}.png"]);
                
                modeCloudMade = new Mode;
                modeCloudMade.templates = Vector.<String>([
                    "http://a.tile.cloudmade.com/BC9A493B41014CAABB98F0471D759707/998/256/${level}/${x}/${y}.png",
                    "http://b.tile.cloudmade.com/BC9A493B41014CAABB98F0471D759707/998/256/${level}/${x}/${y}.png",
                    "http://c.tile.cloudmade.com/BC9A493B41014CAABB98F0471D759707/998/256/${level}/${x}/${y}.png"]);
                
                modeEsri = new Mode;
                modeEsri.templates = Vector.<String>([
                    "http://server.arcgisonline.com/ArcGIS/rest/services/World_Imagery/MapServer/tile/${level}/${y}/${x}.png",
                    "http://services.arcgisonline.com/ArcGIS/rest/services/World_Imagery/MapServer/tile/${level}/${y}/${x}.png"]);
                
                modeList = new ArrayCollection;
                modeList.addItem({label: "Map Quest", data: modeMapQuest});
                modeList.addItem({label: "OSM", data: modeOSM});
                modeList.addItem({label: "MapBox", data: modeMapBox});
                modeList.addItem({label: "CloudMade", data: modeCloudMade});
                modeList.addItem({label: "ESRI", data: modeEsri});
                
                placeholder.addChild(new Stats());
            }
            
            private function get currentComponent():YCanvasStarlingComponent
            {
                var starlingPoint:Point = new Point(stage.mouseX - engine.viewPort.x, stage.mouseY - engine.viewPort.y);
                return engine.stage.hitTest(starlingPoint) as YCanvasStarlingComponent;
            }
            
            private function onClick():void
            {
                if(!currentComponent)
                    return;
                
                var globalPoint:Point = new Point(mouseX, mouseY);
                var ycanvasPoint:Point = currentComponent.controller.globalToViewPort(globalPoint);
            }
            
            private function onRootCreated(event:starling.events.Event, root:starling.display.Sprite):void
            {
                var container:starling.display.Sprite = new starling.display.Sprite();
                //container.x = 300;
                //container.y = 200;
                root.addChild(container);
                
                mapBig = new YCanvasStarlingComponent(modeEsri, stage);
                mapBig.dispatcher.addEventListener(CanvasEvent.CENTER_CHANGED, onMapBigCenterChanged);
                mapBig.dispatcher.addEventListener(CanvasEvent.SCALE_CHANGED, onMapBigScaleChanged);
                mapBig.dispatcher.addEventListener(CanvasEvent.ROTATION_CHANGED, onMapBigRotationChanged);
                mapBig.dispatcher.addEventListener(CanvasEvent.RENDERED, onMapBigRendered);
                container.addChild(mapBig);
                
                mapSmall = new YCanvasStarlingComponent(modeOSM, stage);
                container.addChild(mapSmall);
                
                resizeComponents();
                
                placeholder.addEventListener(ResizeEvent.RESIZE, onPlaceholderResize);
                
                componentList = new ArrayCollection;
                componentList.addItem({label: "Big Map", data: mapBig});
                componentList.addItem({label: "Small Map", data: mapSmall});
                
                stage.addEventListener(MouseEvent.MOUSE_UP, enableMapInteradtion);
            }
            
            private function resizeComponents():void
            {
                var global:Point = placeholder.localToGlobal(new Point(0, 0));
                var w:uint = placeholder.width;
                var h:uint = placeholder.height;
                engine.viewPort = new Rectangle(global.x, global.y, w, h);
                
                mapBig.stage.stageWidth = w;
                mapBig.stage.stageHeight = h;
                
                mapBig.x = 0;
                mapBig.y = 0;
                mapBig.width = w;
                mapBig.height = h;
                
                mapSmall.x = 20;
                mapSmall.y = h- h / 6 - 20;
                mapSmall.width = w / 6;
                mapSmall.height = h / 6;
                
                syncSmallMap();
                if(syncCheckBox.selected)
                    syncSmallMap();
            }
            
            private function syncSmallMap():void
            {
                var globalPoint:Point = mapSmall.controller.canvasToGlobal(mapSmall.controller.center);
                mapSmall.controller.center = mapBig.controller.globalToCanvas(globalPoint);
                mapSmall.controller.scale = mapBig.controller.scale;
                mapSmall.controller.rotation = mapBig.controller.rotation;
                mapSmall.controller.render();
            }
            
            private function syncLatLon():void
            {
                latInput.value = GeoUtils.y2lat(mapBig.controller.center.y);
                lonInput.value = GeoUtils.x2lon(mapBig.controller.center.x);
            }
            
            private function onUpdateComponentClick():void
            {
                var component:YCanvasStarlingComponent = componentSelector.selectedItem.data;
                var mode:Mode = modeSelector.selectedItem.data;
                component.mode = mode;
            }
            
            private function onPlaceholderResize(event:ResizeEvent):void
            {
                resizeComponents();
            }
            
            private function onSyncCheckBoxChange():void
            {
                if(syncCheckBox.selected)
                    syncSmallMap();
            }
            
            private function onMapBigCenterChanged(event:CanvasEvent):void
            {
                if(syncCheckBox.selected)
                    syncSmallMap();
                syncLatLon();
            }
            
            private function onMapBigScaleChanged(event:CanvasEvent):void
            {
                if(syncCheckBox.selected)
                    syncSmallMap();
            }
            
            private function onMapBigRotationChanged(event:CanvasEvent):void
            {
                if(syncCheckBox.selected)
                    syncSmallMap();
            }
            
            private function onMapBigRendered(event:CanvasEvent):void
            {
                if(syncCheckBox.selected)
                    mapSmall.dispatcher.dispatchEvent(new CanvasEvent(CanvasEvent.TRANSFORMATION_FINISHED));
            }
            
            private function onZoomInClick():void
            {
                mapBig.transformationManager.scaleByTween(1.5);
            }
            
            private function onZoomOutClick():void
            {
                mapBig.transformationManager.scaleByTween(1 / 1.5);
            }
            
            private function onNavigateClick():void
            {
                mapBig.transformationManager.moveToTween(GeoUtils.lon2x(lonInput.value), GeoUtils.lat2y(latInput.value));
            }
            
            private function onRotationSliderChange(event:flash.events.Event):void
            {
                //pBig.transformationManager.allowMove = false;
                
                var rotation:Number = rotationInput.value * 0.0174532925;
                //TransformationUtils.rotateTo(mapBig.controller, rotation);
                mapBig.transformationManager.rotateToTween(rotation);
            }
            
            private function disableMapInteraction():void
            {
                mapBig.transformationManager.allowMove = false;
            }
            
            private function enableMapInteradtion(...rest):void
            {
                mapBig.transformationManager.allowMove = true;
            }
        ]]>
    </fx:Script>
    
    <s:layout>
        <s:HorizontalLayout gap="0"/>
    </s:layout>
    
    <s:VGroup width="200">
        <s:DropDownList id="componentSelector" dataProvider="{componentList}" requireSelection="true" width="100%"/>
        <s:DropDownList id="modeSelector" dataProvider="{modeList}" requireSelection="true" width="100%"/>
        <s:Button label="Update Tile Provider" click="onUpdateComponentClick()"/>
        <mx:HRule width="100%"/>
        <s:CheckBox id="syncCheckBox" change="onSyncCheckBoxChange()" label="sync small and big map" selected="true"/>
        <mx:HRule width="100%"/>
        <s:HGroup width="100%">
            <s:NumericStepper id="latInput" minimum="-90" maximum="90" snapInterval="0.001" width="100%"/>
            <s:NumericStepper id="lonInput" minimum="-180" maximum="180" snapInterval="0.001" width="100%"/>
        </s:HGroup>
        
        <s:Button label="Navigate" click="onNavigateClick()"/>
    </s:VGroup>
    
    <s:Group width="100%" height="100%">
        <mx:UIComponent id="placeholder" width="100%" height="100%"/>
        
        <s:HSlider id="rotationInput" minimum="-180" maximum="180" bottom="20" right="80" snapInterval="10"
                   focusEnabled="false" change="onRotationSliderChange(event)"
                   mouseDown="disableMapInteraction()"/>
        
        <s:VGroup bottom="20" right="20" gap="0" mouseDown="disableMapInteraction()">
            <s:Button label="+" width="30" click="onZoomInClick()"/>
            <s:Button label="-" width="30" click="onZoomOutClick()"/>
        </s:VGroup>
    </s:Group>
</s:Application>
